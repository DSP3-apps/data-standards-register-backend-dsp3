apply plugin: 'idea'
apply plugin: 'java'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

group 'openregister'

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile dropwizard, stringTemplate, apacheCommonsCodec, awsCloudWatch
    compile jdbi, aws_cloudsearch, jersey_client, logging_api, logging_impl

    compile(postgresClient) {
        exclude group: 'org.slf4j'
    }

    testCompile junit, mockito, dropwizardTest, wiremock
}

jar.baseName = "openregister-java"

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        } {
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }

        manifest {
            attributes(
                    "Main-Class": "uk.gov.RegisterApplication",
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}

assemble {
    doLast {
        new File("deploy/openregister-java.jar").bytes = new File("build/libs/openregister-java.jar").bytes
        createDeployableBundle.execute()
    }
}

task createDeployableBundle(type: Zip) {
    Map env = System.getenv()
    baseName("openregister-java-${env.'TRAVIS_BRANCH'}-${env.'TRAVIS_BUILD_NUMBER'}-${env.'TRAVIS_COMMIT'}")
    from('deploy/')
    include('*')
    include('scripts/*')
    destinationDir file('deployable_bundle') // directory that you want your archive to be placed in
}
